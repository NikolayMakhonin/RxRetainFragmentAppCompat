apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3'
    }
}
def vName = project.property('version')
def libName = project.property('name')
group = project.property('group')
version = vName

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName vName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile 'io.reactivex:rxjava:1.1.0'
}

task sourcesJar(type: Jar) {
    from files('src/main/java')
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}


bintray {
    user = project.property("bintrayUser")
    key = project.property("bintrayApiKey")
    configurations = ['archives']
    publish = true

    pkg {
        name = libName
        userOrg = user
        repo = libName

        websiteUrl = project.property('siteUrl')
        vcsUrl = project.property('gitUrl')
        licenses = ["Apache-2.0"]

        publicDownloadNumbers = true
        version {
            name = project.version
            desc = ''
            vcsTag = project.version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
            //Optional version-level attributes
        }
    }
}
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                url project.property('siteUrl')
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id project.property('bintrayUser')
                        name project.property("user")
                        email project.property('email')
                    }
                }
                scm {
                    connection project.property('gitUrl')
                    developerConnection project.property('gitUrl')
                    url project.property('siteUrl')

                }

                issueManagement {
                    system 'github'
                    url project.property('siteUrl') + '/issues'
                }
            }
        }
    }
}
